buildscript {
  ext.kotlinVersion = '1.3.21'
  ext.junitVersion = '4.12'
  ext.assertjVersion = '3.10.0'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'
apply from: "gradle/integration-test.gradle"

// Workaround for having both Groovy + Kotlin.
compileGroovy {
  dependsOn tasks.getByPath('compileKotlin')
  classpath += files(compileKotlin.destinationDir)
}

codeQualityTools {
  ktlint {
    toolVersion = '0.24.0'
  }
  detekt {
    toolVersion = '1.0.0.RC7'
  }
  findbugs {
    enabled = false
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    mavenPublishPlugin {
      id = 'com.vanniktech.maven.publish'
      implementationClass = 'com.vanniktech.maven.publish.MavenPublishPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api localGroovy()
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  def dokkaVersion = '0.9.17'
  runtime "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokkaVersion}"
  runtime "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"

  testImplementation "junit:junit:${junitVersion}"
  testImplementation "org.assertj:assertj-core:${assertjVersion}"
  testImplementation 'com.android.tools.build:gradle:3.3.1'
  testImplementation 'com.github.stefanbirkner:system-rules:1.18.0'
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  integrationTestImplementation gradleTestKit()
  integrationTestImplementation "junit:junit:${junitVersion}"
  integrationTestImplementation "org.assertj:assertj-core:${assertjVersion}"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    mavenPublishPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'android', 'kotlin', 'maven', 'publish', 'library']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '5.2.1'
  distributionType = Wrapper.DistributionType.ALL
}
