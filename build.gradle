buildscript {
  ext.kotlinVersion = '1.4.32'
  ext.junitVersion = '4.13.2'
  ext.assertjVersion = '3.19.0'
  ext.agpVersion = '3.6.0'
  ext.dokkaVersion = '0.9.18'
  ext.retrofitVersion = '2.9.0'
  ext.moshiVersion = '1.12.0'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.15.1'
  }
}

apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: "com.vanniktech.maven.publish"
apply from: "gradle/integration-test.gradle"

codeQualityTools {
  ktlint {
    toolVersion = '0.40.0'
  }
  detekt {
    toolVersion = '1.16.0'
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    mavenPublishPlugin {
      id = 'com.vanniktech.maven.publish'
      implementationClass = 'com.vanniktech.maven.publish.MavenPublishPlugin'
    }
    mavenPublishBasePlugin {
      id = 'com.vanniktech.maven.publish.base'
      implementationClass = 'com.vanniktech.maven.publish.MavenPublishBasePlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  api gradleApi()
  api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  compileOnly "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
  compileOnly "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"
  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  compileOnly "com.android.tools.build:gradle:$agpVersion"

  kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

  implementation "com.squareup.moshi:moshi:$moshiVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

  testImplementation "junit:junit:$junitVersion"
  testImplementation "org.assertj:assertj-core:$assertjVersion"
  testImplementation "com.android.tools.build:gradle:$agpVersion"
  testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  testImplementation "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
  testImplementation "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaVersion"

  integrationTestImplementation gradleTestKit()
  integrationTestImplementation "junit:junit:$junitVersion"
  integrationTestImplementation "org.assertj:assertj-core:$assertjVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

wrapper {
  gradleVersion = '7.0'
  distributionType = Wrapper.DistributionType.BIN
}

configurations.all {
  resolutionStrategy {
    force "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  }
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}
